module Rental where 

import Payment
import DA.Time (wholeDays, subTime)

template RentalAgreement 
  with 
    tenant: Party 
    owner: Party 
    operator: Party
    address: Text 
    features: Text
    end: Time 
    start: Time 
    rate: Decimal
    currency: Text  
  where 
    signatory tenant, owner, --operator

    controller owner can 
      EndRental: ContractId Property 
        do 
          now <- getTime 
          assert (now > end)
          create Property 
            with 
              optPublic = None 
              optRate = Some (currency, rate)
              ..

template Offer 
  with 
    tenant: Party 
    owner: Party 
    operator: Party 
    address: Text 
    features: Text
    end: Time 
    start: Time 
    rate: Decimal
    currency: Text
  where 
    signatory owner, operator 

    controller tenant can 
      Accept: (ContractId RentalAgreement, ContractId Payable)
        do 
          payable <- create Payable 
            with 
              to = owner 
              from = tenant 
              amount = rate * intToDecimal (wholeDays (end `subTime` start))
              due = start
              memo = "Rental of " <> address <> " from " <> show start <> " to " <> show end <> "."
              ..

          rental <- create RentalAgreement with ..

          return (rental, payable)

      Decline: ContractId Property 
        do 
          create Property 
            with 
              optPublic = None 
              optRate = None
              ..

template Property 
  with 
    owner: Party 
    address: Text 
    features: Text 
    operator: Party 
    optPublic: Optional Party 
    optRate: Optional (Text, Decimal) 
  where 
    signatory operator, owner 
    observer optPublic

    key (operator, owner, address): (Party, Party, Text)
    maintainer key._1

    controller owner can 
      OfferToTenant: ContractId Offer 
        with 
          tenant: Party 
          start: Time 
          end: Time 
        do 
          let 
            Some (currency, rate) = optRate 
          create Offer with ..

      SetRate: ContractId Property 
        with 
          rate: Decimal 
          currency: Text 
        do 
          create this 
            with optRate = Some (currency, rate)

      Publish: ContractId Property
        with 
          public: Party 
        do 
          create this with optPublic = Some public 

      Unpublish: ContractId Property 
        do create this with optPublic = None

template PropertyKycRequest 
  with 
    owner: Party 
    address: Text 
    features: Text 
    operator: Party 
    proofOfOwnership: Text
  where 
    signatory owner 

    controller operator can 
      VerifyProperty: ContractId Property 
        do 
          create Property
            with 
              optPublic = None 
              optRate = None
              ..

template IndicationOfInterest
  with 
    tenant: Party 
    owner: Party
    address: Text 
    operator: Party 
    start: Time 
    end: Time 
    currency: Text 
    message: Text
  where 
    signatory tenant
    observer operator

    controller owner can 
      MakeAnOffer: ContractId Offer 
        with 
          rate: Decimal 
        do 
          exerciseByKey @Property (operator, owner, address) OfferToTenant with ..